apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "hk.collaction.freehkkai"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1160201 // 0 = release, 1 = beta
        versionName "0.2.1"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            buildConfigField "String", "ADMOB_BANNER_ID", "\"" + getSecretKey("ADMOB_BANNER_ID") + "\""
            buildConfigField "String[]", "DEVICE_ID", getSecretKey("DEVICE_ID")
        }
        staging {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ADMOB_BANNER_ID", "\"" + getSecretKey("ADMOB_BANNER_ID") + "\""
            buildConfigField "String[]", "DEVICE_ID", getSecretKey("DEVICE_ID") + "\""
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "ADMOB_BANNER_ID", "\"" + getSecretKey("ADMOB_BANNER_ID") + "\""
            buildConfigField "String[]", "DEVICE_ID", getSecretKey("DEVICE_ID")
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }
}

static def getSecretKey(name) {
    Properties props = new Properties()
    File f = new File('./../Key/freehkkai.secrets.properties');
    if (f.exists()) {
        props.load(new FileInputStream(f))
        return props[name]
    } else {
        return name
    }
}

dependencies {
    // Google Android Support
    compile 'com.android.support:multidex:1.0.2'
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:preference-v14:27.0.2'

    // Calligraphy
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'

    // Google Play Services
    compile 'com.google.android.gms:play-services-ads:11.8.0'

    // Roboto TextView
    compile 'com.github.johnkil.android-robototextview:robototextview:4.0.0'

    // RxBinding Butter Knife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Material Dialog
    compile 'com.afollestad.material-dialogs:core:0.9.6.0'

    // Keyboard Visibility Event
    compile 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.1.0'
}
